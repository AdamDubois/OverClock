/**
 * --------------------------------------------------------------------------------------------------------------------
 * Example sketch/program showing how to read data from more than one PICC to serial.
 * --------------------------------------------------------------------------------------------------------------------
 * This is a MFRC522 library example; for further details and other examples see: https://github.com/miguelbalboa/rfid
 *
 * Example sketch/program showing how to read data from more than one PICC (that is: a RFID Tag or Card) using a
 * MFRC522 based RFID Reader on the Arduino SPI interface.
 *
 * Warning: This may not work! Multiple devices at one SPI are difficult and cause many trouble!! Engineering skill
 *          and knowledge are required!
 *
 * @license Released into the public domain.
 *
 * Typical pin layout used:
 * -----------------------------------------------------------------------------------------
 *             MFRC522      Arduino       Arduino   Arduino    Arduino          Arduino
 *             Reader/PCD   Uno/101       Mega      Nano v3    Leonardo/Micro   Pro Micro
 * Signal      Pin          Pin           Pin       Pin        Pin              Pin
 * -----------------------------------------------------------------------------------------
 * RST/Reset   RST          9             5         D9         RESET/ICSP-5     RST
 * SPI SS 1    SDA(SS)      ** custom, take a unused pin, only HIGH/LOW required **
 * SPI SS 2    SDA(SS)      ** custom, take a unused pin, only HIGH/LOW required **
 * SPI MOSI    MOSI         11 / ICSP-4   51        D11        ICSP-4           16
 * SPI MISO    MISO         12 / ICSP-1   50        D12        ICSP-1           14
 * SPI SCK     SCK          13 / ICSP-3   52        D13        ICSP-3           15
 *
 * More pin layouts for other boards can be found here: https://github.com/miguelbalboa/rfid#pin-layout
 *
 */

#include <Arduino.h>
#include <SPI.h>
#include <MFRC522.h>

#include <Wire.h> //Communication I2C entre les esp32 et le PI
#include <string.h> //Pour la manipulation des strings

//-----------------------------------------------------------------//
//Constantes et définitions
#define SLAVE_ADDR 0x11  // Adresse de l'esclave
#define SDA_PIN 6
#define SCL_PIN 7

// Broches compatibles ESP32-C3 (adapter si besoin)
#define SCK_PIN        4    // SPI SCK
#define MOSI_PIN       10    // SPI MOSI (ex-6, mais 6 utilisé pour I2C)
#define MISO_PIN       5    // SPI MISO
#define RST_PIN        0    // Reset MFRC522
#define SS_1_PIN       3    // SS MFRC522 #1
#define SS_2_PIN       1    // SS MFRC522 #2
#define SS_3_PIN       18    // SS MFRC522 #3
#define SS_4_PIN       19    // SS MFRC522 #4
#define NR_OF_READERS  4

//byte ssPins[] = {SS_1_PIN, SS_2_PIN, SS_3_PIN, SS_4_PIN};
byte ssPins[] = {SS_1_PIN, SS_2_PIN, SS_3_PIN, SS_4_PIN};

SPIClass spiSPI;
MFRC522 mfrc522[NR_OF_READERS];   // Create MFRC522 instance.

String const ESP32_NAME = "I2C_RFID"; //Le nom de l'ESP32 pour l'identification sur le réseau I2C
//#################################################################//

//-----------------------------------------------------------------//
//Variables globales

//Strings contenant les UID des cartes lues
String g_uidReaders[NR_OF_READERS] = {"NONE", "NONE", "NONE", "NONE"};
String g_stringComplet = "";
//#################################################################//

void dump_byte_array(byte *buffer, byte bufferSize);
void onRequest();
void scanReaders();
void scanReadersNoNew();
String getUIDsAsString(byte *buffer, byte bufferSize);
void requestScanReaders();

/**
 * Initialize.
 */

void setup() {
  Serial.begin(9600); // Initialize serial communications with the PC
  while (!Serial);    // Do nothing if no serial port is opened (added for Arduinos based on ATMEGA32U4)

  // Initialisation de l'I2C en tant qu'esclave avec l'adresse définie
  Wire.setPins(SDA_PIN, SCL_PIN);
  Wire.begin(SLAVE_ADDR);
  Wire.onRequest(onRequest);
  Serial.println("Slave prêt, en attente de requêtes du maître...");

  // Initialiser SPI matériel avec broches compatibles ESP32-C3
  SPI.begin(SCK_PIN, MISO_PIN, MOSI_PIN, -1); // -1: pas de SS global

  for (uint8_t reader = 0; reader < NR_OF_READERS; reader++) {
    mfrc522[reader].PCD_Init(ssPins[reader], RST_PIN); // Utilise la signature compatible
    Serial.print(F("Reader "));
    Serial.print(reader);
    Serial.print(F(": "));
    mfrc522[reader].PCD_DumpVersionToSerial();
  }
}

/**
 * Main loop.
 */
void loop() {
}

/**
 * Helper routine to dump a byte array as hex values to Serial.
 */
void dump_byte_array(byte *buffer, byte bufferSize) {
  for (byte i = 0; i < bufferSize; i++) {
    Serial.print(buffer[i] < 0x10 ? " 0" : " ");
    Serial.print(buffer[i], HEX);
  }
}

void onRequest() {
  Serial.println("Maître a demandé les données.");

  for (int i = 0; i < NR_OF_READERS; i++) {
    g_uidReaders[i] = "NONE";
  }

  requestScanReaders();

  g_stringComplet = "{\"ESP32_NAME\":" + ESP32_NAME + ","; // Réinitialiser la chaîne avec le nom de l'ESP32

  for (int i = 0; i < NR_OF_READERS; i++) {
    g_stringComplet += "\"Reader_" + String(i) + "\":\"" + g_uidReaders[i] + "\"";
    if (i < NR_OF_READERS - 1) {
      g_stringComplet += ",";
    }
  }
  g_stringComplet += "}";

  Wire.write(g_stringComplet.c_str()); // Envoyer les données lues
  Wire.write((uint8_t)0x00); // Indicateur de fin de message
  Serial.println("Données envoyées au maître.");
}

void scanReaders() {
  for (uint8_t reader = 0; reader < NR_OF_READERS; reader++) {
    // Look for new cards

    if (mfrc522[reader].PICC_IsNewCardPresent() && mfrc522[reader].PICC_ReadCardSerial()) {
      Serial.print(F("Reader "));
      Serial.print(reader);
      // Show some details of the PICC (that is: the tag/card)
      Serial.print(F(": Card UID:"));
      dump_byte_array(mfrc522[reader].uid.uidByte, mfrc522[reader].uid.size);
      Serial.println();
      Serial.print(F("PICC type: "));
      MFRC522::PICC_Type piccType = mfrc522[reader].PICC_GetType(mfrc522[reader].uid.sak);
      Serial.println(mfrc522[reader].PICC_GetTypeName(piccType));

      // Halt PICC
      mfrc522[reader].PICC_HaltA();
      // Stop encryption on PCD
      mfrc522[reader].PCD_StopCrypto1();
    } //if (mfrc522[reader].PICC_IsNewC
  } //for(uint8_t reader
}

void scanReadersNoNew() {
  for (uint8_t reader = 0; reader < NR_OF_READERS; reader++) {
    // Wake up ALL cards (including halted ones)
    byte bufferATQA[2];
    byte bufferSize = sizeof(bufferATQA);
    MFRC522::StatusCode status = mfrc522[reader].PICC_WakeupA(bufferATQA, &bufferSize);
    
    if (status == MFRC522::STATUS_OK) {
      if (mfrc522[reader].PICC_ReadCardSerial()) {
        Serial.print(F("Reader "));
        Serial.print(reader);
        Serial.print(F(": Card UID:"));
        dump_byte_array(mfrc522[reader].uid.uidByte, mfrc522[reader].uid.size);
        Serial.println();
        Serial.print(F("PICC type: "));
        MFRC522::PICC_Type piccType = mfrc522[reader].PICC_GetType(mfrc522[reader].uid.sak);
        Serial.println(mfrc522[reader].PICC_GetTypeName(piccType));

        // Halt PICC
        mfrc522[reader].PICC_HaltA();
        // Stop encryption on PCD
        mfrc522[reader].PCD_StopCrypto1();
      }
    }
  }
}

void requestScanReaders() {
  for (uint8_t reader = 0; reader < NR_OF_READERS; reader++) {
    // Wake up ALL cards (including halted ones)
    byte bufferATQA[2];
    byte bufferSize = sizeof(bufferATQA);
    MFRC522::StatusCode status = mfrc522[reader].PICC_WakeupA(bufferATQA, &bufferSize);
    
    if (status == MFRC522::STATUS_OK) {
      if (mfrc522[reader].PICC_ReadCardSerial()) {
        Serial.print(F("Reader "));
        Serial.print(reader);
        Serial.print(F(": Card UID:"));
        g_uidReaders[reader] = getUIDsAsString(mfrc522[reader].uid.uidByte, mfrc522[reader].uid.size);  
        Serial.println();

        // Halt PICC
        mfrc522[reader].PICC_HaltA();
        // Stop encryption on PCD
        mfrc522[reader].PCD_StopCrypto1();
      }
    }
  }
}

String getUIDsAsString(byte *buffer, byte bufferSize) {
  String uidString = "";
  for (byte i = 0; i < bufferSize; i++) {
    uidString += String(buffer[i] < 0x10 ? "0" : "");
    uidString += String(buffer[i], HEX);
  }
  uidString.toUpperCase();
  Serial.println(uidString);
  return uidString;
}